buildscript {
  ext.repos = {
    mavenCentral()
    mavenLocal()
    maven {
      url "https://repo.spring.io/libs-milestone"
    }
  }

  repositories repos

  dependencies {
    classpath(
        'no.skatteetaten.aurora.gradle.plugins:aurora-gradle-plugin:1.2.1'
    )
  }
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.21'
  id 'org.jetbrains.kotlin.plugin.spring' version '1.3.21'
  id 'org.springframework.boot' version '2.1.2.RELEASE'
  id 'org.jlleitschuh.gradle.ktlint' version '6.3.0'
}

ext.aurora = [
    requireStaging          : false,
    applyCheckstylePlugin   : false,
    applySpockSupport       : false,
    setProjectVersionFromGit: false
]

apply plugin: 'io.spring.dependency-management'
apply plugin: 'no.skatteetaten.plugins.aurora'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

repositories repos

[jar, distZip]*.enabled = true
[bootJar, distTar, bootDistTar, bootDistZip]*.enabled = false
configurations.archives.artifacts.removeIf { !it.archiveTask.enabled }

group = groupId

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:2.1.0.RELEASE"
  }
}

def junitJupiterVersion = dependencyManagement.importedProperties['junit-jupiter.version']
dependencies {

  compile(
      "uk.q3c.rest:hal-kotlin:0.5.3.2.a87244c",
      'com.squareup.okhttp3:okhttp:3.13.1',
      'org.jetbrains.kotlin:kotlin-reflect:1.3.21',
      'org.jetbrains.kotlin:kotlin-stdlib-jdk8',
      'com.fasterxml.jackson.module:jackson-module-kotlin',
      'com.fasterxml.jackson.datatype:jackson-datatype-jsr310',
      'no.skatteetaten.aurora.springboot:aurora-spring-boot2-starter:1.2.6',
      'io.fabric8:openshift-client:4.1.1',
      'com.fkorotkov:kubernetes-dsl:1.2.1',
      "org.springframework.retry:spring-retry",
      'org.springframework.boot:spring-boot-starter-webflux',
      'org.springframework.boot:spring-boot-starter-security',
      'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1',
      'org.springframework.boot:spring-boot-starter-hateoas',
      'com.graphql-java-kickstart:graphql-spring-boot-starter:5.4.1',
      'com.graphql-java-kickstart:graphiql-spring-boot-starter:5.4.1',
      'com.graphql-java-kickstart:graphql-java-tools:5.4.1',
      'com.github.fge:json-patch:1.9',
      'org.springframework.boot:spring-boot-devtools'
  )

  [
      'org.springframework.restdocs:spring-restdocs-mockmvc',
      'org.junit.jupiter:junit-jupiter-api',
      "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}",
      'org.springframework.boot:spring-boot-starter-test',
      'com.willowtreeapps.assertk:assertk-jvm:0.13',
      'org.springframework.cloud:spring-cloud-starter-contract-stub-runner',
      'org.springframework.security:spring-security-test',
      'io.projectreactor:reactor-test',
      'com.nhaarman:mockito-kotlin:1.6.0'
  ].each {
    testCompile(it) {
      exclude group: 'junit'
    }
  }

  testImplementation(
      'org.junit.jupiter:junit-jupiter-api',
      'com.squareup.okhttp3:mockwebserver:3.13.1',
      'io.mockk:mockk:1.9',
      'net.bytebuddy:byte-buddy:1.9.3',
      'net.bytebuddy:byte-buddy-agent:1.9.3'
  )
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

version "${version}"

springBoot {
  buildInfo()
}

compileKotlin {
  kotlinOptions {
    suppressWarnings = true
    jvmTarget = 1.8
    freeCompilerArgs = ["-Xjsr305=strict"]
  }
}

compileTestKotlin {
  kotlinOptions {
    suppressWarnings = true
    jvmTarget = 1.8
  }
}

test {
  useJUnitPlatform()
}

//noinspection GroovyAssignabilityCheck
task testExclude(type: Test) {
  if (project.hasProperty('tags')) {
    useJUnitPlatform {
      excludeTags tags.split(',')
    }
  } else {
    useJUnitPlatform()
  }
}

//noinspection GroovyAssignabilityCheck
task testOnly(type: Test) {
  if (project.hasProperty('tags')) {
    useJUnitPlatform {
      includeTags tags.split(',')
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.7'
}

ktlint {
  version = '0.29.0'
}
