buildscript {
  ext.repos = {
    mavenCentral()
    jcenter()
    mavenLocal()

    maven {
      url "https://repo.spring.io/libs-milestone"
    }
  }

  repositories repos

  dependencies {
    classpath(
        'no.skatteetaten.aurora.gradle.plugins:aurora-gradle-plugin:1.2.1'
    )
  }
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.2.70'
  id 'org.jetbrains.kotlin.plugin.spring' version '1.2.70'
  id 'org.springframework.boot' version '2.0.2.RELEASE'
  id 'org.jlleitschuh.gradle.ktlint' version '4.0.0'
  id 'com.github.ben-manes.versions' version '0.20.0'
}

ext.aurora = [
    requireStaging   : false,
    applySpockSupport: false
]

apply plugin: 'io.spring.dependency-management'
apply plugin: 'no.skatteetaten.plugins.aurora'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

repositories repos

[jar, distZip]*.enabled = true
[bootJar, distTar, bootDistTar, bootDistZip]*.enabled = false
configurations.archives.artifacts.removeIf { !it.archiveTask.enabled }

group = 'no.skatteetaten.aurora'

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:2.0.0.RELEASE"
  }
}

def junitJupiterVersion = dependencyManagement.importedProperties['junit-jupiter.version']
dependencies {

  compile(

      'org.jetbrains.kotlin:kotlin-reflect:1.2.70',
      'org.jetbrains.kotlin:kotlin-stdlib-jdk8',
      'com.fasterxml.jackson.module:jackson-module-kotlin',
      'com.fasterxml.jackson.datatype:jackson-datatype-jsr310',
      'no.skatteetaten.aurora.springboot:aurora-spring-boot2-starter:1.2.3',
      'io.fabric8:openshift-client:3.1.12',
      'com.fkorotkov:kubernetes-dsl:1.2.1',
      "org.springframework.retry:spring-retry",
      'org.springframework.boot:spring-boot-starter-webflux',
      'org.springframework.boot:spring-boot-starter-security',
      'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.25.3',
      'org.springframework.boot:spring-boot-starter-hateoas',
      'com.graphql-java-kickstart:graphql-spring-boot-starter:5.0.4',
      'com.graphql-java-kickstart:graphiql-spring-boot-starter:5.0.4',
      'com.graphql-java-kickstart:graphql-java-tools:5.3.3',
      'com.github.fge:json-patch:1.9',
      'org.springframework.boot:spring-boot-devtools'
  )

  [
      'org.springframework.restdocs:spring-restdocs-mockmvc',
      'org.junit.jupiter:junit-jupiter-api',
      "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}",
      'org.springframework.boot:spring-boot-starter-test',
      'io.mockk:mockk:1.8.7',
      'com.willowtreeapps.assertk:assertk:0.10',
      'org.springframework.cloud:spring-cloud-starter-contract-stub-runner',
      'org.springframework.security:spring-security-test',
      'io.projectreactor:reactor-test'
  ].each { testCompile(it) { exclude group: 'junit' } }

  testImplementation(
          "org.junit.jupiter:junit-jupiter-api",
          "com.squareup.okhttp3:mockwebserver:3.11.0"
  )
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

springBoot {
  buildInfo()
}

compileKotlin {
  kotlinOptions {
    suppressWarnings = true
    jvmTarget = 1.8
    freeCompilerArgs = ["-Xjsr305=strict"]
  }
}

compileTestKotlin {
  kotlinOptions {
    suppressWarnings = true
    jvmTarget = 1.8
  }
}

kotlin {
  experimental {
    coroutines "enable"
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.7'
}

ktlint {
  version = '0.23.1'
}
